Use EstimateHistory_Globals.pkg
Use EstimateHistory_Login.dg
Use cCrossEstimateFiltersEstmast.dd
Use EstimateManagementFilters.dg
Use cEstmastQueryBuilder.pkg
Use HCSS_cHTTPCJGrid_Sortable.pkg
Use GenericPassword.dg
Use Windows.pkg

Cd_Popup_Object oManageArchivedEstimates is a HCSS_ModalPanel
	Set Label to "Manage Archived Estimates"
	Set Size to 326 462
	Set piMinSize to 326 462
	Set Sysmenu_Icon to False
	Set Border_Style to Border_Thick
	Set piView_Purpose to CI_EST_HIST
	Set Help_Id to hi_EstimateHistory_Manage

	Property Boolean pbPasswordValidated
	Property Boolean pbArchivedEstimateView True

	Property EH_Estimate[] 	patEstimates
	Property Handle 		phoEstimateHistoryQueryBuilder 	0
	Property String			psOrderBy						"Code" 				// For later on when we add row ordering, must match the server side property name
	Property String			psOrderDirection				CS_QUERYORDERBY_ASC // For later on when we add row ordering, must be asc (CS_QUERYORDERBY_ASC) or desc (CS_QUERYORDERBY_desc)
	Property String         psSearchTerm                    ""
	Property Handle phEstimateList

	Object oEstimateHistoryQueryBuilder is a cEstmastQueryBuilder
		Move Self to giTemp
		Set phoEstimateHistoryQueryBuilder to giTemp
	End_Object // oEstimateHistoryQueryBuilder

	Object oEstMastDD is a cCrossEstimateFiltersEstmastDD
	End_Object // oEstMastDD

	Object oSearchForm is a HCSS_Form
		Set Size to 13 100
		Set Location to 9 7
		Set ResizeMode to RS_ANCHOR_TOP_LEFT

		On_Key Key_Enter Send InitiateSearch
	End_Object // oSearchForm

	Object oSearchButton is a HCSS_Button
		Set Size to 14 42
		Set Location to 9 108
		Set Label to "Search"
		Set ResizeMode to RS_ANCHOR_TOP_LEFT

		Procedure OnClick
			Send InitiateSearch
		End_Procedure // OnClick
	End_Object // oSearchButton

	Object oClearButton is a HCSS_Button
		Set Size to 14 42
		Set Location to 9 152
		Set Label to "Clear"
		Set ResizeMode to RS_ANCHOR_TOP_LEFT

		Procedure OnClick
			Set Value of oSearchForm to ""
			Set psSearchTerm to ""
			Send ResetSearchFilters of (phoEstimateHistoryQueryBuilder(Self))
			Send LoadInitialData of oEstimatesGrid
		End_Procedure // OnClick
	End_Object // oClearButton

	Procedure ChangeEstimateTypeUI Boolean bIsArchivedView
	    Boolean bOldState
	    
	    Get Dynamic_Update_State to bOldState
	    Set Dynamic_Update_State to False
	    
		If (bIsArchivedView) Set pbArchivedEstimateView to True
		Else Set pbArchivedEstimateView to False

		//Grid
		Send ToggleColumnVisibility of oEstimatesGrid bIsArchivedView

		//Buttons
		If (bIsArchivedView) Begin
			Set Visible_State of oCopyButton to True
			Set Visible_State of oRestoreButton to True
			Set Visible_State of oRemoveButton to False
		End
		Else Begin
			Set Visible_State of oCopyButton to False
			Set Visible_State of oRestoreButton to False
			Set Visible_State of oRemoveButton to True
		End
		
		Set Dynamic_Update_State to bOldState
	End_Procedure // ChangeEstimateTypeUI

	Object oFailureHelpLink is a FCP_HyperLink
		Set Size to 10 190
		Set Location to 11 200
		Set TextColor to RGB_RED

		Set ResizeMode to RS_ANCHOR_TOP_LEFT
		Set Label to "Some estimates failed to process. Click here for more info."

		Procedure OnClick
			Integer iMainHelp_Id
			Delegate Get Help_Id to iMainHelp_Id
			Delegate Set Help_ID to hi_EstimateHistory_Manage_ProcessingFailure
			Send Help
			Delegate Set Help_ID to iMainHelp_Id
		End_Procedure // OnClick
	End_Object // oFailureHelpLink

	Object oEstimateType is a HCSS_RadioGroup
		Set Location to 1 348
		Set Size to 24 108
		Set Label to "Type"
		Set TextColor to clAqua
		Set ResizeMode to RS_ANCHOR_TOP_RIGHT

		Object oRadArchived is a HCSS_Radio
			Set Label to "Archived"
			Set Size to 10 45
			Set Location to 11 13
		End_Object // oRadArchived

		Object oRadLive is a HCSS_Radio
			Set Label to "Live"
			Set Size to 10 30
			Set Location to 11 69
		End_Object // oRadLive

		Procedure Notify_Select_State Integer iToItem Integer iFromItem
			Integer iCurrentRadio
			Forward Send Notify_Select_State iToItem iFromItem
			Get Current_Radio to iCurrentRadio
			Send ChangeEstimateTypeUI (If(iCurrentRadio = 0, True, False))

			If (IsComObjectCreated(oEstimatesGrid(Self))) Begin
				Send MoveToFirstRow of oEstimatesGrid
				Send Activate of oEstimatesGrid
				Send LoadInitialData of oEstimatesGrid
			End
		End_Procedure // Notify_Select_State
	End_Object // oEstimateType

	Procedure ResetEstimatesProperty
		EH_Estimate[] atBlank
		Set patEstimates to atBlank
	End_Procedure // ResetEstimatesProperty

	Procedure ResetEstimatesList
		handle hEstimateList
		get phEstimateList to hEstimateList
		if (hEstimateList) Send Destroy of hEstimateList
		Get Create (refclass(cKeyValueList)) to hEstimateList
		Set pvDefaultReturnValue of hEstimateList to False
		Clear Estmast
		repeat
			find gt Estmast by index.1
			if (found) Begin
				Send SetValue of hEstimateList (Rtrim(Estmast.EST#)) True
			End
		Until (not(Found))
		set phEstimateList to hEstimateList
	End_Procedure // ResetEstimatesList

	Object oEstimatesGrid is a HCSS_cHTTPCJGrid_Sortable
		Set Size to 249 449
		Set Location to 27 7
		Set ResizeMode to RS_RESIZE_OBJECT

		Set psNoFieldsAvailableText to "All columns are visible."
		Set pbReadOnly 				to True
		Set pbAllowColumnRemove 	to True
		Set pbAllowColumnResize 	to True
		Set pbAllowColumnReorder 	to True
		Set piDefaultColumnIndex    to 4

		Function GetProcessedStatusDescription String sStatus Returns String
			If (sStatus = "Queued") Function_Return "Queued for Processing"
			If (sStatus = "QueuedLarge") Function_Return "Queued for Processing (Due to estimate size, processing will occur at night)"
			If (sStatus = "Processing") Function_Return "Processing"
			If (sStatus = "Success") Function_Return ""
			If (sStatus = "Failure") Function_Return "Failure Processing. Click here for more information."
			Function_Return ""
		End_Function // GetProcessedStatusDescription

		Function GetErrorDetailDescription String sStatus Returns String
			If (sStatus = "BlobStorage") Function_Return "Failure: Error finding estimate"
			If (sStatus = "ExtractEstimateData") Function_Return "Failure: Error reading estimate data"
			If (sStatus = "SaveEstimateData") Function_Return "Failure: Error saving analysis data"
			If (sStatus = "Attachments") Function_Return "Failure: Error saving attachments"

			Function_Return "Failure: Unable to process estimate"
		End_Function // GetErrorDetailDescription
		
		Procedure LoadInitialData
		    Send ResetEstimatesProperty
			Set Visible_State of oFailureHelpLink to False
		    Forward Send LoadInitialData
        End_Procedure

		Function LoadRemoteData Integer iTop Integer iSkip Returns tDataSourceRow[]
			EH_Estimate[] atEstimates atLoadedEstimates
			tDataSourceRow[] atData
			Integer i iSize iIDCol iCodeCol iNameCol iStatusCol iVersionCol iProcessColumn iDetailColumn iBidDateColumn iCreatedDateColumn iLastModifiedColumn iProcessedSuccessfullyColumn iLastUploadedColumn
			String sQueryString sOrderBy sOrderDirection
			Handle hQueryBuilder
			TSortableGridColumnInfo tSortInfo

			Send Cursor_Wait of Cursor_Control

			Get phoEstimateHistoryQueryBuilder to hQueryBuilder

			Set Top of hQueryBuilder to iTop
			Set Skip of hQueryBuilder to iSkip

			Get ptCurrentSortableColumn to tSortInfo

			If (tSortInfo.bAscending) Move CS_QUERYORDERBY_ASC to sOrderDirection
			Else Move CS_QUERYORDERBY_DESC to sOrderDirection

			Move tSortInfo.sFieldName to sOrderBy
			Send ResetOrder of hQueryBuilder
			Send AddQueryOrderBy of hQueryBuilder sOrderBy sOrderDirection

			Send ResetRouteParams of hQueryBuilder
			if (not(pbArchivedEstimateView(Self))) Begin
				Send AddQueryRouteParam of hQueryBuilder "activeEstimates" "true" CI_QUERYVALUE_BOOL
			End

			Get QueryString of hQueryBuilder to sQueryString
			Get AllEstimates of ghoEstimateHistoryService sQueryString to atEstimates

			Get patEstimates to atLoadedEstimates
			Get ConcatArray atLoadedEstimates atEstimates to atLoadedEstimates
			Set patEstimates to atLoadedEstimates

			Move (SizeOfArray(atEstimates) - 1) to iSize

			Get piColumnId of oIDColumn to iIDCol
			Get piColumnId of oCodeColumn to iCodeCol
			Get piColumnId of oNameColumn to iNameCol
			Get piColumnId of oStatusColumn to iStatusCol
			Get piColumnId of oProcessedColumn to iProcessColumn
			Get piColumnId of oDetailColumn to iDetailColumn
			Get piColumnId of oVersionColumn to iVersionCol
			Get piColumnId of oBidDateColumn to iBidDateColumn
			Get piColumnId of oCreatedDateColumn to iCreatedDateColumn
			Get piColumnId of oLastModifiedColumn to iLastModifiedColumn
			Get piColumnId of oLastUploadedColumn to iLastUploadedColumn
			Get piColumnId of oProcessedSuccessfullyColumn to iProcessedSuccessfullyColumn

			For i from 0 to iSize
				Move atEstimates[i].sEstimateId to atData[i].sValue[iIDCol]
				Move atEstimates[i].sEstimateCode to atData[i].sValue[iCodeCol]
				Move atEstimates[i].sName to atData[i].sValue[iNameCol]
				Move "Y" to atData[i].sValue[iStatusCol]
				If (atEstimates[i].sProcessedStatus = "Success") Begin
					Move "Y" to atData[i].sValue[iProcessColumn]
					Move "Y" to atData[i].sValue[iProcessedSuccessfullyColumn]
					Get GetProcessedStatusDescription atEstimates[i].sProcessedStatus to atData[i].sValue[iDetailColumn]
				End
				Else Begin
					Move "N" to atData[i].sValue[iProcessColumn]
					If (atEstimates[i].sProcessedStatus = "Failure") Begin
						Move "N" to atData[i].sValue[iProcessedSuccessfullyColumn]
						Get GetErrorDetailDescription atEstimates[i].sProcessedStatusFailureCode to atData[i].sValue[iDetailColumn]
						Set Visible_State of oFailureHelpLink to True
					End
					Else Get GetProcessedStatusDescription atEstimates[i].sProcessedStatus to atData[i].sValue[iDetailColumn]
				End

				Move atEstimates[i].sEstimateVersion to atData[i].sValue[iVersionCol]
				Move atEstimates[i].dBid_Date to atData[i].sValue[iBidDateColumn]
				Move atEstimates[i].dCreated_Date to atData[i].sValue[iCreatedDateColumn]
				Move atEstimates[i].dModified_Date to atData[i].sValue[iLastModifiedColumn]
				Move atEstimates[i].dUploaded_Date to atData[i].sValue[iLastUploadedColumn]
			Loop

			Send Cursor_Ready of Cursor_Control

			Function_Return atData
		End_Function // LoadRemoteData

		Procedure ConditionalColorError Handle hoGridItemMetrics
			Set ComForeColor of hoGridItemMetrics to clRed
			Set piSelectedRowForeColor to clRed
			Set piHighlightForeColor to clRed
		End_Procedure // ConditionalColorError

		Procedure ConditionalColorUntracked Handle hoGridItemMetrics
			Set ComForeColor of hoGridItemMetrics to clTeal
			Set piSelectedRowForeColor to clTeal
			Set piHighlightForeColor to clTeal
		End_Procedure // ConditionalColorUntracked

		Procedure ConditionalColorReset Handle hoGridItemMetrics
			Set ComForeColor of hoGridItemMetrics to clBlack
			Set piSelectedRowForeColor to clBlack
			Set piHighlightForeColor to clBlack
		End_Procedure // ConditionalColorReset

		Procedure ConditionalFormatting Handle hoGridItemMetrics Integer iRow String ByRef sValue
			String sCode sError
			Boolean bCodeExistsLocally
			Handle hEstimateList

			Get RowValue of oProcessedSuccessfullyColumn iRow to sError
			If (sError = "N") Begin
				Send ConditionalColorError hoGridItemMetrics
				Procedure_Return
			End

			If (not(pbArchivedEstimateView(Self))) Begin
				Get phEstimateList to hEstimateList
				Get RowValue of oCodeColumn iRow to sCode
				Get ValueFromKey of hEstimateList sCode to bCodeExistsLocally

				If (not(bCodeExistsLocally)) Begin
					Move "Estimate does not exist locally." to sValue
					Send ConditionalColorUntracked hoGridItemMetrics
					Procedure_Return
				End
			End

			Send ConditionalColorReset hoGridItemMetrics
		End_Procedure // ConditionalFormatting

		Object oRowIndicator_Column is a HCSS_cCJGridColumnRowIndicator
			Set piWidth to 22
			Set pbAllowRemove to False
			Set pbAllowDrag to False
			Set pbShowInFieldChooser to False
			Send IgnoreColumn Self
		End_Object // oRowIndicator_Column

		Object oIDColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "Id"
			Set piWidth to 0
			Set psCaption to "ID"
			Set pbVisible to False
			Set pbShowInFieldChooser to False
		End_Object // oIDColumn

		Object oProcessedSuccessfullyColumn is a HCSS_cCJGridColumn
			Set piWidth to 88
			Set pbCheckbox to True
			Set psCheckboxTrue to "Y"
			Set psCheckboxFalse to "N"
			Set pbVisible to False
			Set pbShowInFieldChooser to False
			Send IgnoreColumn Self
		End_Object // oProcessedSuccessfullyColumn

		Object oCodeColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "Code"
			Set piWidth to 68
			Set psCaption to "Code"
			Set pbAllowRemove to False
		End_Object // oCodeColumn

		Object oNameColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "Name"
			Set piWidth to 260
			Set psCaption to "Name"
		End_Object // oNameColumn

		Object oStatusColumn is a HCSS_cCJGridColumn
			Set piWidth to 88
			Set psCaption to "Uploaded"
			Set pbCheckbox to True
			Set psCheckboxTrue to "Y"
			Set psCheckboxFalse to "N"
			Send IgnoreColumn Self
		End_Object // oStatusColumn

		Object oProcessedColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "ProcessedStatus"
			Set piWidth to 88
			Set psCaption to "Processed"
			Set pbCheckbox to True
			Set psCheckboxTrue to "Y"
			Set psCheckboxFalse to "N"
		End_Object // oProcessedColumn

		Object oVersionColumn is a HCSS_cCJGridColumn_Sortable
			set psFieldName to "EstimateVersion"
			Set piWidth to 0
			Set psCaption to "Estimate Version"
			Set pbVisible to False
			Set pbShowInFieldChooser to False
		End_Object // oVersionColumn

		Object oBidDateColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "Filters/Bid_Date"
			Set piWidth to 70
			Set psCaption to "Bid Date"
			Set pbVisible to False
			Set pbShowInFieldChooser to True
		End_Object // oBidDateColumn

		Object oCreatedDateColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "Filters/Created_Date"
			Set piWidth to 70
			Set psCaption to "Created Date"
			Set pbVisible to False
			Set pbShowInFieldChooser to True
		End_Object // oCreatedDateColumn

		Object oLastModifiedColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "Filters/Modified_Date"
			Set piWidth to 70
			Set psCaption to "Last Modified"
			Set pbVisible to False
			Set pbShowInFieldChooser to True
		End_Object // oLastModifiedColumn

		Object oLastUploadedColumn is a HCSS_cCJGridColumn_Sortable
			Set psFieldName to "LastModified"
			Set piWidth to 70
			Set psCaption to "Last Uploaded"
			Set pbVisible to False
			Set pbShowInFieldChooser to True
		End_Object // oLastUploadedColumn

		Object oDetailColumn is a HCSS_cCJGridColumn
			Set piWidth to 147
			Set psCaption to "Detail"
			Send IgnoreColumn Self

			Procedure OnSetDisplayMetrics Handle hoGridItemMetrics Integer iRow String  ByRef sValue
				Send ConditionalFormatting hoGridItemMetrics iRow (&sValue)
				Forward Send OnSetDisplayMetrics hoGridItemMetrics iRow (&sValue)
			End_Procedure // OnSetDisplayMetrics
		End_Object // oDetailColumn
		
		Procedure OnCreate
		    Forward Send OnCreate
		    Send ToggleColumnVisibility (pbArchivedEstimateView(Self))
        End_Procedure

		Procedure RestoreLayout
			Forward Send RestoreLayout
			Send ToggleColumnVisibility (pbArchivedEstimateView(Self))
		End_Procedure // RestoreLayout

		Procedure ToggleColumnVisibility Boolean bIsArchiveView
			If (bIsArchiveView) Begin
				Set pbVisible of oLastUploadedColumn to False
				Set pbVisible of oStatusColumn to True
			End
			Else Begin
				Set pbVisible of oLastUploadedColumn to True
				Set pbVisible of oStatusColumn to False
			End
		End_Procedure // ToggleColumnVisibility
	End_Object // oEstimatesGrid

	Object oCustomizeButton is a HCSS_Button
		Set Location to 281 7
		Set Label to "Customize"
		Set ResizeMode to RS_ANCHOR_BOTTOM_LEFT

		Procedure OnClick
			Send Activate of oEstimatesGrid
			Send CustomizeGrid of oEstimatesGrid
		End_Procedure // OnClick
	End_Object // oCustomizeButton

	Object oFiltersButton is a HCSS_Button
		Set Location to 281 62
		Set Label to "Fi&lters"
		Set ResizeMode to RS_ANCHOR_BOTTOM_LEFT

		Procedure OnClick
			Integer iFilters_Set
			String sQueryString
			Handle hDD

			Move (oEstMastDD(Self)) to hDD
			Get Popup_CrossEstimate_Filters hDD (oManageArchivedEstimates(Self)) 0 False to iFilters_Set
			If (iFilters_Set) Send UpdateFiltersUI True
		End_Procedure // OnClick
	End_Object // oFiltersButton

	Object oRefreshButton is a HCSS_Button
		Set Location to 281 406
		Set Label to "Re&fresh"
		Set ResizeMode to RS_ANCHOR_BOTTOM_RIGHT

		Procedure OnClick
			Send MakeNewRequest
		End_Procedure // OnClick
	End_Object // oRefreshButton

	Function ValidatePassword Boolean bSkipIfAlreadyValidated Returns Boolean
		Integer iActualPassword iEnteredPassword
		Boolean bValidPassword
		String sLabel

		If ((bSkipIfAlreadyValidated) and (pbPasswordValidated(Self))) Function_Return True

		#IFDEF CB_UGMVERSION
		Get HCSS_Password_To_Int gsUGMInternalPassword to iActualPassword
		Move "" to sLabel
		#ELSE
		Get Delete_Estimates_Password_Int to iActualPassword
		Move "(same as Delete Estimate password)" to sLabel
		#ENDIF

		Get PopupGenericPassword iActualPassword "Estimate History Password" sLabel to bValidPassword
		Set pbPasswordValidated to bValidPassword

		Function_Return bValidPassword
	End_Function // ValidatePassword

	Function CanRetrieveCopyArchivedEstimate Returns Boolean
		If (giUserGroupsEnabled) Begin
			If (gsEstimateHistoryAccess = "FULL") Function_Return True

			Send Stop_Box "The user group you are assigned to does not have permission to copy/retrieve archived estimates. Please check your user group settings in Tools/User Admin/Groups." "Estimate History - Read Only Access"
			Function_Return False
		End

		Function_Return (ValidatePassword(Self, True))
	End_Function // CanRetrieveCopyArchivedEstimate

	Object oCopyButton is a HCSS_Button
		Set Size to 14 70
		Set Location to 307 262
		Set Label to 'C&opy Estimate'
		Set ResizeMode to RS_ANCHOR_BOTTOM_RIGHT

		Procedure OnClick
			Boolean bFail
			Integer iSelectedRow
			EH_Estimate[] atEstimates

			Get patEstimates to atEstimates
			Get SelectedRow of (phoDataSource(oEstimatesGrid(Self))) to iSelectedRow
			If (iSelectedRow <> -1 and iSelectedRow < SizeOfArray(atEstimates)) Begin
				Get RetrieveEstimate atEstimates[iSelectedRow] True to bFail
				If (not(bFail) ) Send Close_Panel
			End
		End_Procedure // OnClick
	End_Object // oCopyButton

	Object oRestoreButton is a HCSS_Button
		Set Size to 14 70
		Set Location to 307 334
		Set Label to '&Retrieve Estimate'
		Set ResizeMode to RS_ANCHOR_BOTTOM_RIGHT

		Procedure OnClick
			Boolean bFail
			Integer iSelectedRow eResponse
			EH_Estimate[] atEstimates

			Get YesNo_Box "Retrieving an archived estimate removes the online historical data and moves the estimate from the online archive to the local system.\nDo you want to continue?" "Retrieve Archived Estimate" MB_DEFBUTTON1 to eResponse
			If (eResponse = MBR_Yes) Begin
				Get patEstimates to atEstimates
				Get SelectedRow of (phoDataSource(oEstimatesGrid(Self))) to iSelectedRow
				If (iSelectedRow <> -1 and iSelectedRow < SizeOfArray(atEstimates)) Begin
					Get RetrieveEstimate atEstimates[iSelectedRow] False to bFail
					If (not(bFail)) Send Close_Panel
				End
			End
		End_Procedure // OnClick
	End_Object // oRestoreButton

	Object oRemoveButton is a HCSS_Button
		Set Size to 14 70
		Set Location to 307 334
		Set Label to 'R&emove Estimate'
		Set ResizeMode to RS_ANCHOR_BOTTOM_RIGHT
		Set Visible_State to False

		Procedure OnClick
			Boolean bFail
			Integer iSelectedRow eResponse
			EH_Estimate[] atEstimates

			If (not(pbArchivedEstimateView(Self))) Begin
				Get YesNo_Box "Removing an estimate will remove live estimate data from the HeavyBid Insights website. However, the estimate will remain an active estimate in your HeavyBid system. \n\nDo you want to continue to remove this estimate's live data from HeavyBid Insights?" "Remove Live Estimate" MB_DEFBUTTON1 to eResponse
				If (eResponse = MBR_Yes) Begin
					Get patEstimates to atEstimates
					Get SelectedRow of (phoDataSource(oEstimatesGrid(Self))) to iSelectedRow
					If (iSelectedRow <> -1 and iSelectedRow < SizeOfArray(atEstimates)) Begin
						Get DeleteEstimate atEstimates[iSelectedRow] to bFail
					End
				End
				Send MakeNewRequest
			End

		End_Procedure // OnClick
	End_Object // oRemoveButton

	Object oCloseButton is a HCSS_Button
		Set Location to 307 406
		Set Label to '&Close'
		Set ResizeMode to RS_ANCHOR_BOTTOM_RIGHT

		Procedure OnClick
			Send Close_Panel
		End_Procedure // OnClick
	End_Object // oCloseButton

	Procedure InitiateSearch
		String sSearchValue

		Get Value of oSearchForm to sSearchValue
		Move (Trim(sSearchValue)) to sSearchValue
		Set psSearchTerm to sSearchValue
		Send MakeNewRequest
	End_Procedure // InitiateSearch

	Procedure MakeNewRequest
		String sSearchValue
		Handle hQueryBuilder

		Get psSearchTerm to sSearchValue
		If (sSearchValue = "") Begin
			Send KeyAction of oClearButton
			Procedure_Return
		End

		Get phoEstimateHistoryQueryBuilder to hQueryBuilder
		Send ResetSearchFilters of hQueryBuilder
		Send AddQueryFilter of hQueryBuilder "Code" "" sSearchValue CI_QUERYFILTER_SEARCH CI_QUERYVALUE_STRING
		Send AddQueryFilter of hQueryBuilder "Name" "" sSearchValue CI_QUERYFILTER_SEARCH CI_QUERYVALUE_STRING
		Send LoadInitialData of oEstimatesGrid
	End_Procedure // MakeNewRequest

	Procedure UpdateFiltersUI Boolean bUpdateGrid
		Handle hDD hQueryBuilder

		Move (oEstMastDD(Self)) to hDD
		If (CrossEstimateFiltersAreSet(hDD)) Begin
			Set Label to "Manage Archived Estimates (FILTERS ON)"
			Set Form_FontWeight of oFiltersButton to 5000
		End
		Else Begin
			Set Label to "Manage Archived Estimates"
			Set Form_FontWeight of oFiltersButton to 200
		End

		If (bUpdateGrid) Begin
			Get phoEstimateHistoryQueryBuilder to hQueryBuilder
			Send ResetFilters of hQueryBuilder
			Send ConvertEstmastFiltersToQueryParams of hQueryBuilder hDD
			Send LoadInitialData of oEstimatesGrid
		End
	End_Procedure // UpdateFiltersUI

	Function DeleteEstimate EH_Estimate tEstimate Returns Boolean
		EH_DeleteEstimate tDeletedEstimate

		Send Cursor_Wait of Cursor_Control
		Get DeleteCloudEstimate of ghoEstimateHistoryService tEstimate.sEstimateId False to tDeletedEstimate
		Send Cursor_Ready of Cursor_Control
		If (not(tDeletedEstimate.bSuccess)) Begin
			Send Stop_Box ("Error deleting archived estimate. \nError:\n" * tDeletedEstimate.sDeletionError) "Delete Archived Estimate"
			Function_Return True
		End

		Function_Return False
	End_Function // DeleteEstimate

	Function RetrieveEstimate EH_Estimate tEstimate Boolean bCopyMode Returns Boolean
		Boolean bFail
		EH_DeleteEstimate tDeletedEstimate

		If (CanRetrieveCopyArchivedEstimate(Self)) Begin
			Get RetrieveEstimate of ghoEstimateHistoryService tEstimate bCopyMode to bFail
			If (not(bFail) and not(bCopyMode)) Begin
				Send Cursor_Wait of Cursor_Control
				Get DeleteCloudEstimate of ghoEstimateHistoryService tEstimate.sEstimateId False to tDeletedEstimate
				Send Cursor_Ready of Cursor_Control
				If (not(tDeletedEstimate.bSuccess)) Send Stop_Box ("Error deleting archived estimate. \nError:\n" * tDeletedEstimate.sDeletionError) "Delete Archived Estimate"
			End
		End
		Else Move True to bFail

		Function_Return bFail
	End_Function // RetrieveEstimate

	Procedure Page_Object Boolean bPage
		Forward Send Page_Object bPage
		Send UpdateFiltersUI False
		Set Visible_State of oFailureHelpLink to False
	End_Procedure // Page_Object


	On_Key Key_Alt+Key_E Send KeyAction to (oRemoveButton(Self))
	On_Key Key_Alt+Key_O Send KeyAction to (oCopyButton(Self))
	On_Key Key_Alt+Key_L Send KeyAction to (oFiltersButton(Self))
	On_Key Key_Alt+Key_R Send KeyAction to (oRestoreButton(Self))
	On_Key Key_Alt+Key_C Send KeyAction to (oCloseButton(Self))
	On_Key Key_Alt+Key_F Send KeyAction to (oRefreshButton(Self))
	On_Key KEY_ESCAPE Send KeyAction to (oCloseButton(Self))
Cd_End_Object // oManageArchivedEstimates

Procedure Activate_EstimateHistory_ManageArchivedEstimates
	Handle hModal
	Integer iRetVal

	If (CanActivateEstimateHistory(true, false)) Begin
		Get Popup_Handle of oManageArchivedEstimates to hModal
		Send ResetEstimatesProperty of hModal
		Send ResetEstimatesList of hModal
		Send Popup_Modal of hModal
	End
End_Procedure // Activate_EstimateHistory_ManageArchivedEstimates