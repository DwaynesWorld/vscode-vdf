//JDL 01/10/2017 Removed the word "Revision" from the HeavyBid version description on UGM systems so that it will fit on About screen.
//CWB 10/11/2016 (HB-1467) Don't show Richardson license text if the system only has default empty data.
//CWB 10/11/2016 (HB-1467) Added Richardson year to the about panel.
//VIN 10/05/2016 (HB-1386) Updated HeavyBid with new icon
//KDT 09/26/2016 (HB-1369) Update web link and phone number.
//JDL 03/15/2016 (HB-817) Added build number to Help > About HeavyBid.
//FWC 09/26/2014 Fixed the hover color for the hyperlinks so that they are consistent with the background.
//OWJ 04/25/2014 (OT3788) Add RSMeans Demo Data
//FWC 07/12/2013 (OT2450) Changed Wording on the RSMeans version
//FWC 06/25/2013 (OT2450) Added RSMeans version
//FWC 02/28/2012 (OT1483) Rewrite the Help->About screen.
//CWB 03/02/2011 (OT789) Changed "WSTYPE" to "SIZE" this field name changed when we moved from the registry to XML.
//JPC 10/19/2010 (OT540) Added workstation size indicator (Large/Small).
//FWC 08/29/2009 (8210) Used HeavyBidDefine.pkg constants instead.
//BJS 08/19/2009 (8331) We now allow the possibility of temporary network bonus users.
//MAS 06/10/2009 (7907) Update copyright year for 2009.
//MAS 01/08/2009 (7907) Modified code for the release name to be Pre-Release instead of Beta.
//MAS 12/23/2008 (7856) Changed "Users'" to "User's" to match marketing materials.
//MAS 12/16/2008 (7856) Add code for UGM version.
//BJS 11/21/2008 (7396) System Type is no longer in Report.
//FWC 07/12/2008 (7240) New SQL implementation
//FWC 06/03/2008 (7477) Prevent "&" from showing up as an underscore in Company name.
//MAS 05/15/2008 (7446) Modified code for revision changes to 2008.1.
//FWC 04/17/2008 (7405) Instead of getting the modification date of HeavyBid.exe, we are getting the compiled date.
//MAS 04/07/2008 (7191) Updated copyright year.
//MAS 10/26/2007 (7078) Changed code for revision.
//FWC 06/21/2007 (6862) Made sure we grab the right EXE.
//BJS 06/22/2007 (6938) Updated check on max number of users.
//MAS 01/27/2007 (6686) Modified code for revision changes.
//MAS 11/30/2006 (6592) Changed revision for 2006.12.
//MAS 09/22/2006 (6504) Change revision for 2006.10.
//MAS 08/14/2006 (6472) Change revision for 2006.09.
//MAS 07/12/2006 Change revision for 2006.08.
//MAS 06/01/2006 Change revision for 2006.07.
//MAS 04/21/06  Changed year in a label.
//MAS 03/22/06  (6235) Now using a function call to get HeavyBid.exe modified date for the revision date, rather than using a field in syssetup.dat.
//MAS 03/21/06  (6235) Added Revision date to the panel and moved objects around to accomodate them.
//MAS 02/08/05  (5592) Changed copywrite year.
//FWC 12/30/2004 (4989) Added SQL Indicator.
//MAS 05/24/04  (5275) Modified how revision is displayed - with month name instead of a cryptic number.
//MAS 01/16/04  Changed Copyright year.
//MAS 04/09/03  (4476) Changed copyright year.
//CHS 10/05/02  (3988) Changed the label of oSupport1c to use "e-mail" instead of "E-mail".
//JMK 09/30/02  (3945) Erik Wagner is definitely _NOT_ the bomb....
//JMK 09/27/02  (3945) CHanged about HCSS to about HeavyBid.
//SNM 9/5/2002  (3835) Changed the title bar of this view to "About HCSS"
//MAS 05/30/02  (3588) Added a line in the panel to list our email address.  Had to increase the panel's size and reposition objects.
//MAS 05/30/02  (3587) Now we use SysSetup.Ascii5 instead of manually changing the revision each month.
//MAS 01/07/02  (3257) Took out the words "for Windows" in HeavyBid for Windows, as per MJ.
//MAS 06/01/01  (Dev 2912) Added a changed to running explore, so that it has the split pane when it is launched.  Also, changed 3.0c to 3.0d.
//MAS 04/30/01  CHanged "3.0" to "3.0b"
//MAS 03/01/01  Took out the reference to beta.  Simply 3.0 now.
//SNM 01/02/01  (Dev 2619) Copyright notice updated to 2001.
//ERP 01/20/00  Changed to 2.2A
// BAD 8/23/99  (bug 1631) Testing Bug system. No changes made.
//JMK 12/22/98  Added Build 347 to wording.
//CHS 12/21/98  (Bug 1374) Added code to put "Demo Version" in oVersion for the demo version.
//BAD 10/20/98  (Bug 1178) Added hot key for close button (Alt+C). Commented out (Alt+K) since Close button
//              has replaced OK button.
// BAD 8/14/98  (Bug 904) Add buttons 'system info' to display extra system info and 'explorer' to launch
//              Windows Explorer defaulting to the current estimate. also resized and moved ok btn.
//*****************************************************************************************
//CHS 04/18/97  Changed copyright from 1996 to 1997.  Took out programmers.
//              Added version, company name, users, and support numbers.
//              Changed to HCSS classes.
// BKA 04/18/97 Resized a few things to make sure it looks right in a higher resolution.
// JMK 05/17/97 Changed the phone number to the regular phone number per Mike.  I also
//              took out the 24-hour support number.
//CHS 07/06/97  Changed "BASIC" to "ADVANCED", and "Basic Version" to "Advanced Version"
//CHS 07/07/97  Changed "INTRO" to "BASIC", and "Introductory Version" to "Basic Version"
//JMK 07/08/97  Changed revision 1.1 to 1.1a.
//JMK 11/18/97  Changed revision 1.1a to 1.2
//JMK 1/15/98   Changed revision 1.2 to 1.3
//JMK 1/15/98   Changed revision 1.2 to 1.3b
//JMK 6/30/98   Changed revision 1.3b to 1.4
//JMK 9/30/98   Changed revision 1.4 to 2.0.  I did not put my initials in the code as I change this line too much.
//Register_Function GetFileModTime String sHBPath Returns Date  //MAS 03/22/06
Use HCSSAPI.CLS
Use Win32api.pkg
Use Sysinfo.mod
Use cImageContainer.cls
Use cFlowLayout.cls

CD_Popup_Object AboutPanel  is a ModalPanel
	Set Label to "About HeavyBid"
	Set Size to 214 317
	Set Locate_Mode to CENTER_ON_SCREEN
	Set Help_Id to hi_About_HeavyBid
	Set Color to clWhite

	Object oBigLogo is a cImageContainer
		Set Size to 82 89
		Set Location to 12 12
		Set piTransparentColor to |CI$C0C0C0
	End_Object // oBigLogo

	Object oDescLogo is a BitmapContainer
		Set Size to 41 170
		Set Location to 12 114
		Set Border_Style to Border_None
		Set Color to clWhite
		Set Bitmap to "Welcome.bmp"
	End_Object // oDescLogo

	Object oFlowLayout is a cFlowLayout
		Set piStartX	to 115
		Set piStartY	to 62
		Set piOffsetX  	to 0
		Set piOffsetY 	to 12
		Set piItemPerGroup to 50
		Set pbVertical to True
	End_Object // oFlowLayout

	Object oVersion is a TextBox
		Send AutoPosition of oFlowLayout Self
	End_Object // oVersion

	Object oBuild is a TextBox
		Send AutoPosition of oFlowLayout Self

		Procedure End_Construct_Object
			Handle hoVersionInfo
			Integer iBuild
			Forward Send End_Construct_Object
			Get phoVersionInfo of ghoApplication to hoVersionInfo
			If (pbIncluded(hoVersionInfo)) Move (piVersionBuild(hoVersionInfo)) to iBuild
			Set Label to (sFormat("Build %1",iBuild))
		End_Procedure // End_Construct_Object
	End_Object // oBuild

	Object oDateCreated is a TextBox
		Send AutoPosition of oFlowLayout Self
	End_Object // oDateCreated

	Object oCompany is a TextBox
		Set Window_Style SS_NOPREFIX to TRUE
		Send AutoPosition of oFlowLayout Self
	End_Object // oCompany

	Object oUser_Count is a TextBox
		Send AutoPosition of oFlowLayout Self
	End_Object // oUser_Count

	Object oBonus_Count is a TextBox
		Procedure End_Construct_Object
			Integer iUsers iBonus
			Date dBonus
			Clear SysSetup
			Find GT SysSetup by Recnum
			Get Heavybid_Max_Users to iUsers
			If (iUsers > 1) Begin
				Get HeavyBid_Bonus_Users (&dBonus) to iBonus
			End
			Forward Send End_Construct_Object
			If (iBonus <> 0) Send AutoPosition of oFlowLayout Self
			Else Set Visible_State to False
		End_Procedure // End_Construct_Object
	End_Object // oBonus_Count

	Object oRSMeans_Label is a TextBox
		Procedure End_Construct_Object
			String sFile sYear sQuarter
			Handle hXML
			Boolean bOK
			Forward Send End_Construct_Object
			Move "" to sYear
			Send AutoPosition of oFlowLayout Self
			Move gsEnterprise_Global_SystemPath to sFile
			Move (CombineFilePath(sFile,"RSMeans/VERSION")) to sFile
			If (FileExists(sFile)) Begin
				Get Create U_cXmlDomDocument to hXML
				Set psDocumentName of hXML to sFile
				Get LoadXMLDocument of hXML to bOK
				If (bOK) Begin
					Get ChildNodeValue of hXML "RSMeans/Year" to sYear
					Get ChildNodeValue of hXML "RSMeans/Quarter" to sQuarter
				End
				Send Destroy of hXML
			End
			If (sYear<>"") Set Label to (sFormat("RSMeans %1", (If(gbOwnsRSMeans,("Year"*sYear*"Quarter"*sQuarter),"(Demo Data)"))))
			Else Set Visible_State to False
		End_Procedure // End_Construct_Object
	End_Object // oRSMeans_Label

	Object oRichardson_Label is a TextBox
		Procedure End_Construct_Object
			String sFile sYear
			Handle hXML
			Boolean bOK
			Forward Send End_Construct_Object
			Move "" to sYear
			If (gbOwnsRichardson) Begin
				Send AutoPosition of oFlowLayout Self
				Move gsEnterprise_Global_SystemPath to sFile
				Move (CombineFilePath(sFile,"Richardson/VERSION")) to sFile
				If (FileExists(sFile)) Begin
					Get Create U_cXmlDomDocument to hXML
					Set psDocumentName of hXML to sFile
					Get LoadXMLDocument of hXML to bOK
					If (bOK) Begin
						Get ChildNodeValue of hXML "Richardson/Year" to sYear
					End
					Send Destroy of hXML
				End
			End
			If ((sYear<>"") and (sYear<>"0000")) Set Label to ("Richardson Year" *sYear)
			Else Set Visible_State to False
		End_Procedure // End_Construct_Object
	End_Object // oRichardson_Label

	Object oBottomContainer is a Container3d
		Set Size to 68 317
		Set Location to 147 0
		Set Border_Style to Border_None
		Set Color to clLtGray

		Object oWebLink is a FCP_HyperLink
			Set Size to 10 49
			Set Location to 10 10
			Set Color to clLtGray
			Set HoverColor to clLtGray
			Set Label to "help.hcss.com"

			Procedure OnClick
				Send RunDocument "https://help.hcss.com/s/"
			End_Procedure // OnClick
		End_Object // oWebLink

		Object oDivider is a TextBox
			Set Location to 10 64
			Set Color to clLtGray
			Set Label to "|"
		End_Object // oDivider

		Object oSupportLink is a FCP_HyperLink
			Set Size to 10 62
			Set Location to 10 72
			Set Color to clLtGray
			Set HoverColor to clLtGray
			Set Label to "support@hcss.com"
			Procedure OnClick
				Send RunDocument "mailto:support@hcss.com"
			End_Procedure // OnClick
		End_Object // oSupportLink

		Object oDivider is a TextBox
			Set Location to 10 139
			Set Color to clLtGray
			Set Label to "|"
		End_Object // oDivider

		Object oPhone is a TextBox
			Set Location to 10 146
			Set Color to clLtGray
			Set Label to "(800) 444-3196"
		End_Object // oPhone

		Object oCopyRight is a TextBox
			Set Location to 29 10
			Set Color to clLtGray
			Set Label to (sFormat("Copyright %1 by Heavy Construction Systems Specialists, Inc.",HEAVYBID_YEARLY_VERSION))
		End_Object // oCopyRight

		Object oSmallLogo is a TextBox
			Set Size to 15 23
			Set Location to 27 285
			Set Bitmap to "AboutHCSS.bmp"
			Set Bitmap_Style to Bitmap_Stretch
		End_Object // oSmallLogo

		Object oHDivider is a LineControl
			Set Size to 2 300
			Set Location to 45 9
		End_Object // oHDivider

		Object Explorer_Button is a Button
			Set Label to "Run E&xplorer"
			Set Size to 14 58
			Set Location to 50 8

			Procedure OnClick
				Runprogram Wait "explorer /e," gsEstimateDirectory
			End_Procedure // OnClick
		End_Object // Explorer_Button

		Object Sysinfo_Button is a Button
			Set Label to "System &Info"
			Set Size to 14 58
			Set Location to 50 74
			Procedure OnClick
				Send Popup to (oSystem_Info(Self))
			End_Procedure // OnClick
		End_Object // Sysinfo_Button

		Object Close_Button is a Button
			Set Label to "&Close"
			Set Size to 14 58
			Set Location to 50 251
			Procedure OnClick
				Send Close_Panel
			End_Procedure // OnClick
		End_Object // Close_Button
	End_Object // oBottomContainer

	Procedure Page_Object Integer iI
		Integer iUsers iBonus
		String sVersion sUsers sWSType sBitmap sLogoBitmap
		Date dDate dBonus
		Clear SysSetup
		Find GT SysSetup by Recnum
		If (gi_SystemType = CI_COMP)        Move "Comprehensive" to sVersion
		Else If (gi_SystemType = CI_ADV)    Move "Advanced"      to sVersion
		Else If (gi_SystemType = CI_BASIC)  Move "Basic"         to sVersion
		Else If (gi_SystemType = CI_LITE)   Move "Lite"          to sVersion
		Else If (gi_SystemType = CI_DEMO)   Move "Demo"          to sVersion
		If (gbUsingSQL) Move (sVersion*"SQL") to sVersion
		#IFNDEF CB_UGMVERSION
		Set Value of (oVersion(Self)) to (sFormat("HeavyBid %1 %2", gsVersion, sVersion))
		#ELSE
		Set Value of (oVersion(Self)) to (sFormat("HeavyBid %1 %2 %3", gsVersion, CS_UGMREV, sVersion))
		#ENDIF
		Get GetModuleDateTime TRUE to dDate
		Set Value of (oDateCreated(Self)) to (sFormat("Revision Date is %1", dDate))
		Set Label of (oCompany(Self)) to (sFormat("Licensed to %1", SysSetup.Name))
		Get Heavybid_Max_Users to iUsers
		Set Label of (oUser_Count(Self)) to iUsers
		If (iUsers = 1) Move 'Single User System' to sUsers
		Else Begin
			Move (sFormat("Multiuser System, %1 registered users", iUsers)) to sUsers
			Get_Sys_String ("SIZE") to sWSType
			If (sWSType <> "") Move (sFormat("%1 (%2 Install)", sUsers, (Capitalize(sWSType)))) to sUsers
		End
		Set Value of (oUser_Count(Self)) Item 0 to sUsers
		If (iUsers > 1) Begin
			Get HeavyBid_Bonus_Users (&dBonus) to iBonus
			If (iBonus <> 0) Set Label of oBonus_Count to (sFormat("%1 Bonus Users Until %2", iBonus, dBonus))
		End
		Move "HEAVYBID.BMP" to sLogoBitmap
		Set Bitmap of oBigLogo to sLogoBitmap
		Forward Send Page_Object iI
	End_Procedure // Page_Object

	On_Key Key_Alt+Key_I Send KeyAction to (Sysinfo_Button(Self))
	On_Key Key_Alt+Key_X Send KeyAction to (Explorer_Button(Self))
	On_Key Key_Alt+Key_C Send KeyAction to (Close_Button(Self))
CD_End_Object // AboutPanel